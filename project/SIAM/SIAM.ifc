CONTEXT SIAM

VIEW "SESSION"      : SESSION DEFAULT {user : sessionUserid} ENDVIEW
VIEW OrgAbbrName    : Organization DEFAULT {abbr : orgAbbrName} ENDVIEW
VIEW PersonFullName : Person DEFAULT { first : personFirstName, txt1 : TXT " ", last : personLastName} ENDVIEW
VIEW "Representative" : Representative DEFAULT 
  { firstName : personFirstName
  , txt1 : TXT " "
  , lastName : personLastName
  , txt2 : TXT " ("
  , org : repOrg;orgAbbrName
  , txt3 : TXT ")"
  } ENDVIEW
VIEW Account        : Account DEFAULT {displayName : accDisplayName} ENDVIEW
VIEW Role           : Role DEFAULT {label : label} ENDVIEW
-- VIEW GroupActive    : Group HTML TEMPLATE "View-Checkbox.html" ENDVIEW

API "SessionVars" FOR SYSTEM : I[SESSION] BOX
  [ "sessionAccount"      : sessionAccount
  , "sessionUserid"       : sessionUserid
  , "sessionAllowedRoles" : sessionAllowedRoles
  , "accDisplayName"      : sessionAccount;accDisplayName
  , "accessibleInterfaces": sessionActiveRoles;pf_ifcRoles~
  ]

API "AccessibleInterfaces" FOR SYSTEM: I[SESSION];sessionActiveRoles;pf_ifcRoles~ BOX []
  
--[User INTERFACES]--------------------------------------------------------------------------------
INTERFACE "My account" FOR User : "_SESSION"[SESSION];sessionAccount BOX <TABS>
  [ "Account"        : I[Account] BOX <FORM>
    [ "Username"     : accUserid
    , "Display name" : accDisplayName             cRUd
    , "Allowed roles": accAllowedRoles                                                                             -- account specific roles
                       \/ (repAccount~;active;repOrg \/ accOrg);orgRole                                            -- organization based roles
                      --  \/ account[Subscription*Account]~;(I-(endDateTime;endDateTime~));type;subscriptionTypeRole  -- subscription based roles
    ]
  , "Profile"        : accPerson BOX <FORM>
    [ "First name"   : personFirstName            cRUd
    , "Last name"    : personLastName             cRUd
    , "Emailadres"   : personEmailaddress         cRUd
    ]
  , "Organizations"  : repAccount~ BOX <TABLE>
    [ "Person"       : I <PersonFullName>
    , "Organization" : repOrg
    , "Role"         : repRole
    , "Added"        : tsAdded
    , "Removed"      : tsRemoved
    ]
  -- , "Groups"         : repAccount~;member~ BOX <TABLE>
  --   [ "Group memberships": group
  --   , "Role"             : role
  --   , "Started"          : tsAdded
  --   , "Ended"            : tsRemoved
  --   , "Active group?"    : group;grpActive <GroupActive>
  --   ]
  -- , "Subscriptions"  : account[Subscription*Account]~ BOX <TABLE>
  --   [ "Subscription type" : type
  --   , "Start date"   : startDateTime
  --   , "End date"     : endDateTime
  --   ]
  -- , "Change requests": crSubmitter~ BOX <TABLE sortable sortBy="_35_" order="desc">
  --   [ "#"              : crId
  --   , "Change request" : I
  --   , "Is open"        : crOpen
  --   , "Submitted"      : crDateCreated
  --   , "Assigned to"    : crAssignedTo
  --   ]
  ]

--[Beheerder INTERFACES]---------------------------------------------------------------------------
INTERFACE "View account" FOR Maintainer, Helpdesk : I[Account] BOX <FORM showNavMenu>
  [ "Username"       : accUserid
  , "Display name"   : accDisplayName
  , "Representative" : repAccount~ BOX <TABLE>
    [ "Person"       : I <PersonFullName>
    , "Organization" : repOrg
    , "Role"         : repRole
    , "Added"        : tsAdded
    , "Removed"      : tsRemoved
    ]
  , "Organization"   : accOrg
  , "Allowed roles"  : accAllowedRoles                                                                              -- account specific roles
                        \/ (repAccount~;active;repOrg \/ accOrg);orgRole                                            -- organization based roles
                        -- \/ account[Subscription*Account]~;(I-(endDateTime;endDateTime~));type;subscriptionTypeRole  -- subscription based roles
  -- , "Group memberships" : repAccount~;member~ BOX <TABLE>
  --   [ "Group"        : group
  --   , "Role"         : role
  --   , "Started"      : tsAdded
  --   , "Ended"        : tsRemoved
  --   , "Active group?": group;grpActive <GroupActive>
  --   ]
  -- , "Subscriptions"  : account[Subscription*Account]~ BOX <TABLE>
  --   [ "Subscription type" : type
  --   , "Role"         : type;subscriptionTypeRole
  --   , "Start date"   : startDateTime
  --   , "End date"     : endDateTime
  --   ]
  -- , "Change requests": crSubmitter~ BOX <TABLE sortable sortBy="#" order="desc">
  --   [ "#"              : crId
  --   , "Change request" : I
  --   , "Is open"        : crOpen
  --   , "Submitted"      : crDateCreated
  --   , "Assigned to"    : crAssignedTo
  --   ]
  ]
         
INTERFACE "View organization" FOR Maintainer, Helpdesk : I[Organization] BOX <TABS showNavMenu>
  [ "Organization"   : I BOX <FORM>
    [ "Name"                 : orgAbbrName
    , "Full Name"            : orgFullName
    , "Roles for every user" : orgRole
    , "Domains"              : orgDomain
    , "External identifiers" : orgExternalIdentifier cRud
    , "Representatives"      : repOrg~ BOX <TABLE sortable sortBy="Name" order="asc">
      [ "Name"        : I[Representative] <PersonFullName>
      , "Email"       : personEmailaddress
      , "Role"        : repRole
      , "Active"      : active
      -- , "Active group membership" : member~;active;group
      -- , "Former group membership" : member~;(I-active);group
      ]
    ]
  , "Service accounts" : accOrg~ BOX <TABLE>
    [ "Account"        : I
    , "Account id"     : accUserid
    ]
  -- , "Change requests" : repOrg~;repAccount;crSubmitter~ BOX <TABLE sortable sortBy="_35_" order="desc">
  --   [ "#"              : crId
  --   , "Change request" : I
  --   , "Is open"        : crOpen
  --   , "Submitted"      : crDateCreated
  --   , "Assigned to"    : crAssignedTo
  --   ]
  ]

--[Account management INTERFACES]-----------------------------------------------------------------------
INTERFACE "Organizations" FOR AccountMgr : "_SESSION";V[SESSION*Organization] cRud BOX <TABLE>
  [ "Organization"         : I <OrgAbbrName>
  , "Short name"           : orgAbbrName
  , "Full name"            : orgFullName
  ]
  
INTERFACE "New/edit organization" FOR AccountMgr : I[Organization] CRuD BOX <FORM showNavMenu>
  [ "Short Name"           : orgAbbrName cRUd
  , "Full Name"            : orgFullName cRUd
  , "Roles for every user" : orgRole cRUd
  , "Domains"              : orgDomain cRUd
  , "External identifiers" : orgExternalIdentifier cRud
  , "Representatives"      : repOrg~ CRuD BOX <TABLE>
    [ "First name"    : personFirstName cRUd
    , "Last name"     : personLastName cRUd
    , "Role"          : repRole   cRUd
    , "Added"         : tsAdded   cRud
    , "Removed"       : tsRemoved cRud
    , "Remove"        : removeRequest cRUd
    , "Accounts"      : repAccount cRUd
    , "Details"       : I[Representative] <PersonFullName>
    ]
  , "Service accounts" : accOrg~ BOX <TABLE>
    [ "Account"        : I
    , "Account id"     : accUserid
    ]
  , "View details"         : I cRud LINKTO INTERFACE "View organization"
  ]

INTERFACE "People" FOR AccountMgr : "_SESSION";V[SESSION*Representative] cRud BOX <TABLE sortable>
  [ "Representative"       : I             cRud <PersonFullName>
  , "Organization"         : repOrg        cRud
  , "Role"                 : repRole       cRud
  , "Account"              : repAccount    cRud
  ]

INTERFACE "New/edit person" FOR AccountMgr : I[Person] cRud BOX <FORM showNavMenu>
  [ "First name": personFirstName          cRUd
  , "Last name" : personLastName           cRUd
  , "Initials"  : personInitials           cRUd
  , "Email"     : personEmailaddress       cRUd
  , "Phonenumbers" : personPhoneNumber     cRUd
  ]

INTERFACE "Accounts" FOR AccountMgr : "_SESSION";V[SESSION*Account] cRuD BOX <TABLE sortable sortBy="Last_32_login" order="desc">
  [ "Account"       : I
  , "Represents"    : repAccount~
  , "Org account"   : accOrg
  , "Last login"    : accMostRecentLogin
  ]

INTERFACE "New/edit account" FOR AccountMgr : I[Account] CRuD BOX <FORM showNavMenu>
  [ "UserID"      : accUserid           cRUd
  , "Display name": accDisplayName      cRUd
  , "Represents"  : repAccount~         cRud BOX <TABLE>
    [ "Person"       : I <PersonFullName>
    , "Organization" : repOrg
    , "Role/function": repRole
    , "Added"        : tsAdded
    , "Removed"      : tsRemoved
    , "Roles"        : repOrg;orgRole
    ]
  , "Service account" : accOrg          cRUd BOX <TABLE>
    [ "Organization" : I
    , "Roles"        : orgRole
    ]
  , "Account roles" : accAllowedRoles   cRUd
  , "Last login"  : accMostRecentLogin  cRud
  -- , "Subscriptions"  : account[Subscription*Account]~ BOX <TABLE>
  --   [ "Subscription type" : type
  --   , "Identifier"   : type;id
  --   , "Role"         : type;subscriptionTypeRole
  --   , "Start date"   : startDateTime
  --   , "End date"     : endDateTime
  --   ]
  -- , "Subscription identifiers (for testing)" : accountSubscription cRUd
  , "Allowed roles"  : accAllowedRoles                                                                              -- account specific roles
                        \/ (repAccount~;active;repOrg \/ accOrg);orgRole                                            -- organization based roles
                        -- \/ account[Subscription*Account]~;(I-(endDateTime;endDateTime~));type;subscriptionTypeRole  -- subscription based roles
  ]
  
INTERFACE "Role" FOR AccountMgr : I[Role] cRud BOX <FORM showNavMenu>
  [ "Role"        : I
  , "In use by"   : I BOX <TABS>
    [ "Accounts"    : accAllowedRoles~ cRud BOX <TABLE sortable sortBy="Last_32_login" order="desc">
      [ "Account"      : I
      , "Represents"   : repAccount~
      , "Org account"  : accOrg
      , "Last login"   : accMostRecentLogin
      ]
    , "Organizations" : orgRole~ cRud BOX <TABLE sortable sortBy="Organization" order="asc">
      [ "Organization" : I
      ]
    -- , "Subscription types" : subscriptionTypeRole~ cRud BOX <TABLE sortable sortBy="Subscription_32_type" order="asc">
    --   [ "Subscription type"   : I
    --   , "Community"           : community                   cRud
    --   , "Role"                : subscriptionTypeRole        cRud
    --   ]
    , "Interfaces" : pf_ifcRoles~ cRud BOX <TABLE sortable sortBy="Interface" order="asc">
      [ "Interface"    : I
      , "Label"         : label
      , "Public?"       : isPublic
      , "API?"          : isAPI
      ]
    ]
  ]

INTERFACE "Login history" FOR AccountMgr : I[Account] BOX <TABLE>
  [ "Latest login"        : accMostRecentLogin
  , "Login timestamps"    : accLoginTimestamps
  ]

ENDCONTEXT