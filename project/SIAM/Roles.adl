CONTEXT SIAM

    CONCEPT Role "de naam voor het mogen inzien en/of wijzigen van zekere gegevens, die kan worden toegekend aan accounts en in sessions kan worden geactiveerd" 

    RELATION accAllowedRoles[Account*Role]
    MEANING "An Account registers the Roles that MAY be activated in a SESSION to which the Account is assigned"
    
    RELATION accDefaultRoles[Account*Role]
    MEANING "An Account registers the Roles that ARE activated in a SESSION once the Account is assigned"

    ROLE ExecEngine MAINTAINS "Default roles must be allowed roles"
    RULE "Default roles must be allowed roles": accDefaultRoles |- accAllowedRoles
    VIOLATION (TXT "{EX} DelPair;accDefaultRoles;Account;", SRC I, TXT ";Role;", TGT I)

    --[Account-related Session Variables]--
    ROLE ExecEngine MAINTAINS "Set allowed session roles"
    RULE "Set allowed session roles": sessionAccount;
                                    (  accAllowedRoles                                                                          -- account specific roles
                                    \/ (repAccount~;active;repOrg \/ accOrg);orgRole                                            -- organization based roles
                                    -- \/ account[Subscription*Account]~;(I-(endDateTime;endDateTime~));type;subscriptionTypeRole  -- subscription based roles
                                    )
                                    |- sessionAllowedRoles
    VIOLATION (TXT "{EX} InsPair;sessionAllowedRoles;SESSION;", SRC I, TXT ";Role;", TGT I)

    ROLE ExecEngine MAINTAINS "Clear allowed session roles"
    RULE "Clear allowed session roles": (I /\ sessionAccount;sessionAccount~);sessionAllowedRoles |- sessionAccount;
                                    (  accAllowedRoles                                                                          -- account specific roles
                                    \/ (repAccount~;active;repOrg \/ accOrg);orgRole                                            -- organization based roles
                                    -- \/ account[Subscription*Account]~;(I-(endDateTime;endDateTime~));type;subscriptionTypeRole  -- subscription based roles
                                    ) -- \/ sessionAllowedRoles;"Anonymous"[Role]
    VIOLATION (TXT "{EX} DelPair;sessionAllowedRoles;SESSION;", SRC I, TXT ";Role;", TGT I)

    ROLE ExecEngine MAINTAINS "Activate roles in a session"
    RULE "Activate roles in a session": (I-(sessionActiveRoles;sessionActiveRoles~));sessionAccount;accDefaultRoles |- sessionActiveRoles
    VIOLATION (TXT "{EX} InsPair;sessionActiveRoles;SESSION;", SRC I, TXT ";Role;", TGT I)
    
    ROLE ExecEngine MAINTAINS "Deactivate roles in a session"
    RULE "Deactivate roles in a session": sessionActiveRoles |- sessionAllowedRoles
    VIOLATION (TXT "{EX} DelPair;sessionActiveRoles;SESSION;", SRC I, TXT ";Role;", TGT I)

ENDCONTEXT